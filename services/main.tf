terraform {
  backend "s3" {
    key = "services/terraform.tfstate"
  }
}

provider "aws" {
  region = var.aws_region
}

data "terraform_remote_state" "network" {
  backend = "s3"
  config = {
    bucket = "terraform-state-kubis"
    key    = "network/terraform.tfstate"
    region = "us-east-1"
  }
}

data "terraform_remote_state" "cluster" {
  backend = "s3"
  config = {
    bucket = "terraform-state-kubis"
    key    = "cluster/terraform.tfstate"
    region = "us-east-1"
  }
}

data "terraform_remote_state" "charts" {
  backend = "s3"
  config = {
    bucket = "terraform-state-kubis"
    key    = "charts/terraform.tfstate"
    region = "us-east-1"
  }
}

################################################################################
# CDN (CloudFront)
################################################################################

locals {
  alb_origin_id                = "alb"
  mymlops_alb_origin_id        = "mymlops_alb"
  nathaliacampos_alb_origin_id = "nathaliacampos_alb"
}

data "aws_cloudfront_cache_policy" "no_cache" {
  name = "Managed-CachingDisabled"
}

data "aws_cloudfront_cache_policy" "cache_optimized" {
  name = "Managed-CachingOptimized"
}

data "aws_cloudfront_origin_request_policy" "all_viewer" {
  name = "Managed-AllViewer"
}

resource "aws_cloudfront_distribution" "alb_distribution" {
  origin {
    origin_id   = local.alb_origin_id
    domain_name = data.terraform_remote_state.cluster.outputs.alb_dns_name

    custom_origin_config {
      http_port              = "80"
      https_port             = "443"
      origin_protocol_policy = "match-viewer"
      origin_ssl_protocols   = ["TLSv1", "TLSv1.1", "TLSv1.2"]
    }
  }

  enabled         = true
  is_ipv6_enabled = true
  price_class     = "PriceClass_100"

  aliases = [var.domain, "www.${var.domain}", var.docs_domain, var.blog_domain]

  default_cache_behavior {
    allowed_methods          = ["DELETE", "GET", "HEAD", "OPTIONS", "PATCH", "POST", "PUT"]
    cached_methods           = ["GET", "HEAD"]
    target_origin_id         = local.alb_origin_id
    cache_policy_id          = data.aws_cloudfront_cache_policy.cache_optimized.id
    origin_request_policy_id = data.aws_cloudfront_origin_request_policy.all_viewer.id
    viewer_protocol_policy   = "allow-all"
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    acm_certificate_arn = data.terraform_remote_state.cluster.outputs.acm_certificate_arn
    ssl_support_method  = "sni-only"
  }

  # By default, cloudfront caches error for five minutes. There can be situation when a developer has
  # accidentally broken the website and you would not want to wait for five minutes for the error response to be cached.
  # https://docs.aws.amazon.com/AmazonS3/latest/dev/CustomErrorDocSupport.html
  custom_error_response {
    error_code            = 400
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 403
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 404
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 405
    error_caching_min_ttl = 30
  }
}


resource "aws_cloudfront_distribution" "mymlops_alb_distribution" {
  origin {
    origin_id   = local.mymlops_alb_origin_id
    domain_name = data.terraform_remote_state.cluster.outputs.mymlops_alb_dns_name

    custom_origin_config {
      http_port              = "80"
      https_port             = "443"
      origin_protocol_policy = "match-viewer"
      origin_ssl_protocols   = ["TLSv1", "TLSv1.1", "TLSv1.2"]
    }
  }

  enabled         = true
  is_ipv6_enabled = true
  price_class     = "PriceClass_100"

  aliases = [var.mymlops_domain, "www.${var.mymlops_domain}"]

  # The default behavior is not to cache anything
  default_cache_behavior {
    allowed_methods          = ["DELETE", "GET", "HEAD", "OPTIONS", "PATCH", "POST", "PUT"]
    cached_methods           = ["GET", "HEAD"]
    target_origin_id         = local.mymlops_alb_origin_id
    cache_policy_id          = data.aws_cloudfront_cache_policy.no_cache.id
    origin_request_policy_id = data.aws_cloudfront_origin_request_policy.all_viewer.id
    viewer_protocol_policy   = "allow-all"
  }

  # Cache static files generated by NextJS
  ordered_cache_behavior {
    path_pattern             = "/_next/static/*"
    allowed_methods          = ["GET", "HEAD", "OPTIONS"]
    cached_methods           = ["GET", "HEAD", "OPTIONS"]
    target_origin_id         = local.mymlops_alb_origin_id
    cache_policy_id          = data.aws_cloudfront_cache_policy.cache_optimized.id
    origin_request_policy_id = data.aws_cloudfront_origin_request_policy.all_viewer.id
    viewer_protocol_policy   = "allow-all"
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    acm_certificate_arn = data.terraform_remote_state.cluster.outputs.mymlops_acm_certificate_arn
    ssl_support_method  = "sni-only"
  }

  # By default, cloudfront caches error for five minutes. There can be situation when a developer has
  # accidentally broken the website and you would not want to wait for five minutes for the error response to be cached.
  # https://docs.aws.amazon.com/AmazonS3/latest/dev/CustomErrorDocSupport.html
  custom_error_response {
    error_code            = 400
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 403
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 404
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 405
    error_caching_min_ttl = 30
  }
}

resource "aws_cloudfront_distribution" "nathaliacampos_alb_distribution" {
  origin {
    origin_id   = local.nathaliacampos_alb_origin_id
    domain_name = data.terraform_remote_state.cluster.outputs.nathaliacampos_alb_dns_name

    custom_origin_config {
      http_port              = "80"
      https_port             = "443"
      origin_protocol_policy = "match-viewer"
      origin_ssl_protocols   = ["TLSv1", "TLSv1.1", "TLSv1.2"]
    }
  }

  enabled         = true
  is_ipv6_enabled = true
  price_class     = "PriceClass_100"

  aliases = [var.nathaliacampos_domain, "www.${var.nathaliacampos_domain}"]

  # The default behavior is not to cache anything
  default_cache_behavior {
    allowed_methods          = ["DELETE", "GET", "HEAD", "OPTIONS", "PATCH", "POST", "PUT"]
    cached_methods           = ["GET", "HEAD"]
    target_origin_id         = local.nathaliacampos_alb_origin_id
    cache_policy_id          = data.aws_cloudfront_cache_policy.no_cache.id
    origin_request_policy_id = data.aws_cloudfront_origin_request_policy.all_viewer.id
    viewer_protocol_policy   = "allow-all"
  }

  # Cache static files generated by NextJS
  ordered_cache_behavior {
    path_pattern             = "/_next/static/*"
    allowed_methods          = ["GET", "HEAD", "OPTIONS"]
    cached_methods           = ["GET", "HEAD", "OPTIONS"]
    target_origin_id         = local.nathaliacampos_alb_origin_id
    cache_policy_id          = data.aws_cloudfront_cache_policy.cache_optimized.id
    origin_request_policy_id = data.aws_cloudfront_origin_request_policy.all_viewer.id
    viewer_protocol_policy   = "allow-all"
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    acm_certificate_arn = data.terraform_remote_state.cluster.outputs.nathaliacampos_acm_certificate_arn
    ssl_support_method  = "sni-only"
  }

  # By default, cloudfront caches error for five minutes. There can be situation when a developer has
  # accidentally broken the website and you would not want to wait for five minutes for the error response to be cached.
  # https://docs.aws.amazon.com/AmazonS3/latest/dev/CustomErrorDocSupport.html
  custom_error_response {
    error_code            = 400
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 403
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 404
    error_caching_min_ttl = 30
  }

  custom_error_response {
    error_code            = 405
    error_caching_min_ttl = 30
  }
}


################################################################################
# Domain aliasing
################################################################################

module "dns" {
  source = "git@github.com:kubis-ai/terraform-modules.git//modules/dns"

  domain = var.domain

  # All domains pointing to a CloudFront distribution must also be configured as
  # aliases in the CloudFront settings.
  alias = [
    {
      source  = var.domain,
      target  = aws_cloudfront_distribution.alb_distribution.domain_name
      zone_id = aws_cloudfront_distribution.alb_distribution.hosted_zone_id
    },
    {
      source  = var.docs_domain
      target  = aws_cloudfront_distribution.alb_distribution.domain_name
      zone_id = aws_cloudfront_distribution.alb_distribution.hosted_zone_id
    },
    {
      source  = var.blog_domain
      target  = aws_cloudfront_distribution.alb_distribution.domain_name
      zone_id = aws_cloudfront_distribution.alb_distribution.hosted_zone_id
    },
    {
      source  = var.cicd_domain
      target  = data.terraform_remote_state.cluster.outputs.alb_dns_name
      zone_id = data.terraform_remote_state.cluster.outputs.alb_zone_id
    },
    {
      source  = var.api_domain
      target  = data.terraform_remote_state.cluster.outputs.alb_dns_name
      zone_id = data.terraform_remote_state.cluster.outputs.alb_zone_id
    },
    {
      source  = var.admin_domain
      target  = data.terraform_remote_state.cluster.outputs.alb_dns_name
      zone_id = data.terraform_remote_state.cluster.outputs.alb_zone_id
    },
  ]
}


module "mymlops_dns" {
  source = "git@github.com:kubis-ai/terraform-modules.git//modules/dns"

  domain = var.mymlops_domain

  # All domains pointing to a CloudFront distribution must also be configured as
  # aliases in the CloudFront settings.
  alias = [
    {
      source  = var.mymlops_domain,
      target  = aws_cloudfront_distribution.mymlops_alb_distribution.domain_name
      zone_id = aws_cloudfront_distribution.mymlops_alb_distribution.hosted_zone_id
    },
    {
      source  = var.mymlops_api_domain
      target  = data.terraform_remote_state.cluster.outputs.mymlops_alb_dns_name
      zone_id = data.terraform_remote_state.cluster.outputs.mymlops_alb_zone_id
    },
    {
      source  = var.mymlops_admin_domain
      target  = data.terraform_remote_state.cluster.outputs.mymlops_alb_dns_name
      zone_id = data.terraform_remote_state.cluster.outputs.mymlops_alb_zone_id
    },
  ]
}

module "nathaliacampos_dns" {
  source = "git@github.com:kubis-ai/terraform-modules.git//modules/dns"

  domain = var.nathaliacampos_domain

  # All domains pointing to a CloudFront distribution must also be configured as
  # aliases in the CloudFront settings.
  alias = [
    {
      source  = var.nathaliacampos_domain,
      target  = aws_cloudfront_distribution.nathaliacampos_alb_distribution.domain_name
      zone_id = aws_cloudfront_distribution.nathaliacampos_alb_distribution.hosted_zone_id
    }
  ]
}

################################################################################
# Google Search Console (nathaliacampos.me)
################################################################################

data "aws_route53_zone" "nathaliacampos" {
  name = var.nathaliacampos_domain
}

// This record is for verification on google search console.
resource "aws_route53_record" "google_search_console_txt" {
  zone_id = data.aws_route53_zone.nathaliacampos.zone_id
  name    = "nathaliacampos.me"
  type    = "TXT"
  ttl     = "5"

  records = [
    "google-site-verification=vlh6zhg1Kd5vtGiGBDNfxriCybZTUW4w0eFx40SxHdI"
  ]
}


################################################################################
# Authentication (Firebase)
################################################################################

// Records needed for using custom domain in e-mails sent by Firebase
// and for custom domain hosting using in OAuth flows
data "aws_route53_zone" "kubis" {
  name = var.domain
}

resource "aws_route53_record" "firebase_cname_1" {
  zone_id = data.aws_route53_zone.kubis.zone_id
  name    = "firebase1._domainkey.kubis.ai"
  type    = "CNAME"
  ttl     = "5"

  records = ["mail-kubis-ai.dkim1._domainkey.firebasemail.com."]
}

resource "aws_route53_record" "firebase_cname_2" {
  zone_id = data.aws_route53_zone.kubis.zone_id
  name    = "firebase2._domainkey.kubis.ai"
  type    = "CNAME"
  ttl     = "5"

  records = ["mail-kubis-ai.dkim2._domainkey.firebasemail.com."]
}

resource "aws_route53_record" "firebase_txt" {
  zone_id = data.aws_route53_zone.kubis.zone_id
  name    = "kubis.ai"
  type    = "TXT"
  ttl     = "5"

  records = [
    "v=spf1 include:_spf.firebasemail.com ~all",
    "firebase=aerial-ceremony-330017",
    "google-site-verification=9QIbZRfgIYgDTW_KPR73dxSVTFYOxJrS__oduUlG6Pg"
  ]
}

resource "aws_route53_record" "firebase_auth_domain" {
  zone_id = data.aws_route53_zone.kubis.zone_id
  name    = var.auth_domain
  type    = "A"
  ttl     = "5"

  records = ["199.36.158.100"]
}

resource "aws_ssm_parameter" "firebase_auth_domain" {
  name        = var.firebase_auth_domain_path
  description = "The custom domain used by Firebase for authentication."
  type        = "String"
  value       = var.auth_domain
}

################################################################################
# MyMLOps Authentication (Firebase) + Google Search Console
################################################################################
// Records needed for using custom domain in e-mails sent by Firebase
// and for custom domain hosting using in OAuth flows

data "aws_route53_zone" "mymlops" {
  name = var.mymlops_domain
}

resource "aws_route53_record" "firebase_mymlops_cname_1" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "firebase1._domainkey.mymlops.com"
  type    = "CNAME"
  ttl     = "5"

  records = ["mail-mymlops-com.dkim1._domainkey.firebasemail.com."]
}

resource "aws_route53_record" "firebase_mymlops_cname_2" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "firebase2._domainkey.mymlops.com"
  type    = "CNAME"
  ttl     = "5"

  records = ["mail-mymlops-com.dkim2._domainkey.firebasemail.com."]
}

// The last record is from Google Search Console
resource "aws_route53_record" "firebase_mymlops_txt" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "mymlops.com"
  type    = "TXT"
  ttl     = "5"

  records = [
    "v=spf1 include:_spf.firebasemail.com ~all",
    "firebase=mymlops-f828d",
    "google-site-verification=JI_J3YsaPnTF3c013aZ1wWk2EbPAq6hurYgUUywjTho"
  ]
}

// This setting configures the social login callback url. It can be configured in Firebase
// under Hosting.
resource "aws_route53_record" "firebase_mymlops_auth_domain" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = var.mymlops_auth_domain
  type    = "A"
  ttl     = "5"

  records = ["199.36.158.100"]
}

resource "aws_ssm_parameter" "firebase_mymlops_auth_domain" {
  name        = var.firebase_mymlops_auth_domain_path
  description = "The custom domain used by Firebase for MyMLOps authentication."
  type        = "String"
  value       = var.mymlops_auth_domain
}

################################################################################
# Email (SES)
################################################################################

module "email" {
  source = "git@github.com:kubis-ai/terraform-modules.git//modules/email"

  domain           = var.domain
  email_identities = var.email_identities
  aws_region       = var.aws_region
}

################################################################################
# Email (SES) - MyMLOps
################################################################################

module "mymlops_email" {
  source = "git@github.com:kubis-ai/terraform-modules.git//modules/email"

  domain           = var.mymlops_domain
  email_identities = var.mymlops_email_identities
  aws_region       = var.aws_region
}

################################################################################
# Email (WorkMail) - MyMLOps
################################################################################

resource "aws_route53_record" "mymlops_mx" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = data.aws_route53_zone.mymlops.name
  type    = "MX"
  ttl     = 86400
  records = ["10 inbound-smtp.${var.aws_region}.amazonaws.com."]
}

// enable Autodiscover service for Outlook and other clients
resource "aws_route53_record" "mymlops_autodiscover" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "autodiscover.${data.aws_route53_zone.mymlops.name}"
  type    = "CNAME"
  ttl     = 86400
  records = ["autodiscover.mail.${var.aws_region}.awsapps.com."]
}

// SES identity / verification
resource "aws_ses_domain_identity" "mymlops_identity" {
  domain = data.aws_route53_zone.mymlops.name
}

// DKIM
resource "aws_ses_domain_dkim" "mymlops_dkim" {
  domain = aws_ses_domain_identity.mymlops_identity.domain
}

resource "aws_route53_record" "mymlops_dkim" {
  count   = 3
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "${element(aws_ses_domain_dkim.mymlops_dkim.dkim_tokens, count.index)}._domainkey.${data.aws_route53_zone.mymlops.name}"
  type    = "CNAME"
  ttl     = "600"
  records = ["${element(aws_ses_domain_dkim.mymlops_dkim.dkim_tokens, count.index)}.dkim.amazonses.com."]
}

// DMARC record
resource "aws_route53_record" "mymlops_dmarc" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "_dmarc.${data.aws_route53_zone.mymlops.name}"
  type    = "TXT"
  ttl     = 86400
  records = ["v=DMARC1;p=quarantine;pct=100;fo=1;"]
}


################################################################################
# Email (WorkMail)
################################################################################

data "aws_route53_zone" "primary" {
  name         = var.domain
  private_zone = false
}

resource "aws_route53_record" "mx" {
  zone_id = data.aws_route53_zone.primary.zone_id
  name    = data.aws_route53_zone.primary.name
  type    = "MX"
  ttl     = 86400
  records = ["10 inbound-smtp.${var.aws_region}.amazonaws.com."]
}

// enable Autodiscover service for Outlook and other clients
resource "aws_route53_record" "autodiscover" {
  zone_id = data.aws_route53_zone.primary.zone_id
  name    = "autodiscover.${data.aws_route53_zone.primary.name}"
  type    = "CNAME"
  ttl     = 86400
  records = ["autodiscover.mail.${var.aws_region}.awsapps.com."]
}

// SES identity / verification
resource "aws_ses_domain_identity" "identity" {
  domain = data.aws_route53_zone.primary.name
}

// DKIM
resource "aws_ses_domain_dkim" "dkim" {
  domain = aws_ses_domain_identity.identity.domain
}

resource "aws_route53_record" "dkim" {
  count   = 3
  zone_id = data.aws_route53_zone.primary.zone_id
  name    = "${element(aws_ses_domain_dkim.dkim.dkim_tokens, count.index)}._domainkey.${data.aws_route53_zone.primary.name}"
  type    = "CNAME"
  ttl     = "600"
  records = ["${element(aws_ses_domain_dkim.dkim.dkim_tokens, count.index)}.dkim.amazonses.com."]
}

// DMARC record
resource "aws_route53_record" "dmarc" {
  zone_id = data.aws_route53_zone.primary.zone_id
  name    = "_dmarc.${data.aws_route53_zone.primary.name}"
  type    = "TXT"
  ttl     = 86400
  records = ["v=DMARC1;p=quarantine;pct=100;fo=1;"]
}

################################################################################
# Mailchimp - MyMLOps
################################################################################

resource "aws_route53_record" "mailchimp1" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "k2._domainkey"
  type    = "CNAME"
  ttl     = 600
  records = ["dkim2.mcsv.net"]
}

resource "aws_route53_record" "mailchimp2" {
  zone_id = data.aws_route53_zone.mymlops.zone_id
  name    = "k3._domainkey"
  type    = "CNAME"
  ttl     = 600
  records = ["dkim3.mcsv.net"]
}

################################################################################
# Container registry
################################################################################

module "container_registry" {
  source = "git@github.com:kubis-ai/terraform-modules.git//modules/container-registry"

  repository_list      = var.repository_list
  image_tag_mutability = "IMMUTABLE"
  enable_scan_on_push  = true
}

################################################################################
# Filesystem service
################################################################################

resource "aws_s3_bucket" "filesystem_object_store" {
  bucket = var.filesystem_bucket_name

  # Prevent accidental deletion of this S3 bucket
  lifecycle {
    prevent_destroy = false
  }

  versioning {
    enabled = true
  }

  force_destroy = true

  server_side_encryption_configuration {
    rule {
      apply_server_side_encryption_by_default {
        sse_algorithm = "AES256"
      }
    }
  }
}

resource "aws_iam_user" "filesystem" {
  name = "FilesystemService"
}

resource "aws_iam_access_key" "filesystem" {
  user = aws_iam_user.filesystem.name
}

resource "aws_iam_user_policy" "filesystem_policy" {
  name = "S3AccessForFilesystemService"
  user = aws_iam_user.filesystem.name

  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": "s3:*",
      "Resource": [
        "arn:aws:s3:::*/*",
        "arn:aws:s3:::${aws_s3_bucket.filesystem_object_store.id}"
      ]
    }
  ]
}
EOF
}

resource "aws_ssm_parameter" "filesystem_bucket_name" {
  name        = var.filesystem_bucket_name_path
  description = "The name of the S3 bucket for the Filesystem service."
  type        = "String"
  value       = aws_s3_bucket.filesystem_object_store.id
}

resource "aws_ssm_parameter" "filesystem_endpoint" {
  name        = var.filesystem_endpoint_path
  description = "The S3 endpoint for the Filesystem service."
  type        = "String"
  value       = "s3.${aws_s3_bucket.filesystem_object_store.region}.amazonaws.com"
}

resource "aws_ssm_parameter" "filesystem_access_key_id" {
  name        = var.filesystem_access_key_id_path
  description = "The AWS access key id for the Filesystem service."
  type        = "String"
  value       = aws_iam_access_key.filesystem.id
}

resource "aws_ssm_parameter" "filesystem_secret_access_key" {
  name        = var.filesystem_secret_access_key_path
  description = "The AWS secret access key for the Filesystem service."
  type        = "SecureString"
  value       = aws_iam_access_key.filesystem.secret
}

resource "aws_db_subnet_group" "filesystem_db" {
  name       = "filesystem_db_main"
  subnet_ids = data.terraform_remote_state.network.outputs.private_subnets
}

resource "random_password" "filesystem_db_password" {
  length           = 16
  special          = true
  override_special = "%@"
}

resource "aws_db_instance" "filesystem_db" {
  name = "filesystem_db"

  engine            = "postgres"
  engine_version    = "13.7"
  instance_class    = var.filesystem_db_instance_class
  allocated_storage = var.filesystem_db_allocated_storage

  username = "filesystem_db"
  password = random_password.filesystem_db_password.result
  port     = "5432"

  vpc_security_group_ids = [aws_security_group.allow_traffic_from_cluster.id]
  db_subnet_group_name   = aws_db_subnet_group.filesystem_db.id

  maintenance_window        = "Mon:00:00-Mon:03:00"
  backup_window             = "03:00-06:00"
  skip_final_snapshot       = false
  final_snapshot_identifier = var.filesystem_db_final_snapshot_identifier

  deletion_protection = var.filesystem_db_deletion_protection
}

resource "aws_ssm_parameter" "filesystem_database_connection_uri" {
  name        = var.filesystem_database_connection_uri_path
  description = "The connection URI for the Filesystem service database."
  type        = "SecureString"
  value       = "postgres://${urlencode("${aws_db_instance.filesystem_db.username}")}:${urlencode("${aws_db_instance.filesystem_db.password}")}@${aws_db_instance.filesystem_db.endpoint}/${aws_db_instance.filesystem_db.name}"
}

################################################################################
# Cloud service
################################################################################

resource "aws_security_group" "allow_traffic_from_cluster" {
  name        = "allow_traffic_from_cluster"
  description = "Allow traffic from the cluster nodes."
  vpc_id      = data.terraform_remote_state.network.outputs.vpc_id

  ingress {
    description     = "Allows traffic on port 5432."
    from_port       = 5432
    to_port         = 5432
    protocol        = "tcp"
    security_groups = [data.terraform_remote_state.cluster.outputs.worker_security_group_id]
  }
}

resource "aws_db_subnet_group" "cloud_db" {
  name       = "cloud_db_main"
  subnet_ids = data.terraform_remote_state.network.outputs.private_subnets
}

resource "random_password" "cloud_db_password" {
  length           = 16
  special          = true
  override_special = "%@"
}

resource "aws_db_instance" "cloud_db" {
  name = "cloud_db"

  engine            = "postgres"
  engine_version    = "13.7"
  instance_class    = var.cloud_db_instance_class
  allocated_storage = var.cloud_db_allocated_storage

  username = "cloud_db"
  password = random_password.cloud_db_password.result
  port     = "5432"

  vpc_security_group_ids = [aws_security_group.allow_traffic_from_cluster.id]
  db_subnet_group_name   = aws_db_subnet_group.cloud_db.id

  maintenance_window        = "Mon:00:00-Mon:03:00"
  backup_window             = "03:00-06:00"
  skip_final_snapshot       = false
  final_snapshot_identifier = var.cloud_db_final_snapshot_identifier

  deletion_protection = var.cloud_db_deletion_protection
}

resource "aws_ssm_parameter" "cloud_database_connection_uri" {
  name        = var.cloud_database_connection_uri_path
  description = "The connection URI for the Cloud service database."
  type        = "SecureString"
  value       = "postgres://${urlencode("${aws_db_instance.cloud_db.username}")}:${urlencode("${aws_db_instance.cloud_db.password}")}@${aws_db_instance.cloud_db.endpoint}/${aws_db_instance.cloud_db.name}"
}

resource "aws_iam_user" "cloud" {
  name = "CloudService"
}

resource "aws_iam_access_key" "cloud" {
  user = aws_iam_user.cloud.name
}

resource "aws_iam_policy" "cloud_pricing_policy" {
  name        = "PricingAccessForCloudService"
  description = "This policy gives cloud service permission to request pricing of AWS products."

  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": "pricing:GetProducts",
      "Resource": [
        "*"
      ]
    }
  ]
}
EOF
}

resource "aws_iam_user_policy_attachment" "cloud_pricing_policy_attach" {
  user       = aws_iam_user.cloud.name
  policy_arn = aws_iam_policy.cloud_pricing_policy.arn
}

resource "aws_iam_policy" "cloud_assume_role_policy" {
  name        = "AssumeRoleForCloudService"
  description = "This policy allows cloud service to gain access of users' AWS accounts."

  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": "sts:AssumeRole",
      "Resource": [
        "*"
      ]
    }
  ]
}
EOF
}

resource "aws_iam_user_policy_attachment" "cloud_assume_role_policy_attach" {
  user       = aws_iam_user.cloud.name
  policy_arn = aws_iam_policy.cloud_assume_role_policy.arn
}

resource "aws_ssm_parameter" "cloud_access_key_id" {
  name        = var.cloud_access_key_id_path
  description = "The AWS access key id for the Cloud service."
  type        = "String"
  value       = aws_iam_access_key.cloud.id
}

resource "aws_ssm_parameter" "cloud_secret_access_key" {
  name        = var.cloud_secret_access_key_path
  description = "The AWS secret access key for the Cloud service."
  type        = "SecureString"
  value       = aws_iam_access_key.cloud.secret
}

data "aws_ami" "cpu_machine_image" {
  most_recent = true

  owners = ["045329000471"]

  filter {
    name   = "name"
    values = ["kubis-deep-learning-py36-cpu-*"]
  }
}

resource "aws_ssm_parameter" "aws_cpu_machine_image" {
  name        = var.cloud_aws_cpu_machine_image_path
  description = "The AWS CPU machine image for the Cloud service."
  type        = "String"
  value       = data.aws_ami.cpu_machine_image.id
}

data "aws_ami" "gpu_machine_image" {
  most_recent = true

  owners = ["045329000471"]

  filter {
    name   = "name"
    values = ["kubis-deep-learning-py36-gpu-*"]
  }
}

resource "aws_ssm_parameter" "aws_gpu_machine_image" {
  name        = var.cloud_aws_gpu_machine_image_path
  description = "The AWS GPU machine image for the Cloud service."
  type        = "String"
  value       = data.aws_ami.gpu_machine_image.id
}

resource "aws_ssm_parameter" "cloud_redis_connection_uri" {
  name        = var.cloud_redis_connection_uri_path
  description = "The connection URI for the Cloud service Redis."
  type        = "SecureString"
  value       = data.terraform_remote_state.charts.outputs.redis_connection_uri
}

################################################################################
# Notebook service
################################################################################

resource "aws_db_subnet_group" "notebook_db" {
  name       = "notebook_db_main"
  subnet_ids = data.terraform_remote_state.network.outputs.private_subnets
}

resource "random_password" "notebook_db_password" {
  length           = 16
  special          = true
  override_special = "%@"
}

resource "aws_db_instance" "notebook_db" {
  name = "notebook_db"

  engine            = "postgres"
  engine_version    = "13.7"
  instance_class    = var.notebook_db_instance_class
  allocated_storage = var.notebook_db_allocated_storage

  username = "notebook_db"
  password = random_password.notebook_db_password.result
  port     = "5432"

  vpc_security_group_ids = [aws_security_group.allow_traffic_from_cluster.id]
  db_subnet_group_name   = aws_db_subnet_group.notebook_db.id

  maintenance_window        = "Mon:00:00-Mon:03:00"
  backup_window             = "03:00-06:00"
  skip_final_snapshot       = false
  final_snapshot_identifier = var.notebook_db_final_snapshot_identifier

  deletion_protection = var.notebook_db_deletion_protection
}

resource "aws_ssm_parameter" "notebook_database_connection_uri" {
  name        = var.notebook_database_connection_uri_path
  description = "The connection URI for the Notebook service database."
  type        = "SecureString"
  value       = "postgres://${urlencode("${aws_db_instance.notebook_db.username}")}:${urlencode("${aws_db_instance.notebook_db.password}")}@${aws_db_instance.notebook_db.endpoint}/${aws_db_instance.notebook_db.name}"
}

resource "aws_ssm_parameter" "notebook_redis_connection_uri" {
  name        = var.notebook_redis_connection_uri_path
  description = "The connection URI for the Notebook service Redis."
  type        = "SecureString"
  value       = data.terraform_remote_state.charts.outputs.redis_connection_uri
}

################################################################################
# Billing service
################################################################################

resource "aws_db_subnet_group" "billing_db" {
  name       = "billing_db_main"
  subnet_ids = data.terraform_remote_state.network.outputs.private_subnets
}

resource "random_password" "billing_db_password" {
  length           = 16
  special          = true
  override_special = "%@"
}

resource "aws_db_instance" "billing_db" {
  name = "billing_db"

  engine            = "postgres"
  engine_version    = "13.7"
  instance_class    = var.billing_db_instance_class
  allocated_storage = var.billing_db_allocated_storage

  username = "billing_db"
  password = random_password.billing_db_password.result
  port     = "5432"

  vpc_security_group_ids = [aws_security_group.allow_traffic_from_cluster.id]
  db_subnet_group_name   = aws_db_subnet_group.billing_db.id

  maintenance_window        = "Mon:00:00-Mon:03:00"
  backup_window             = "03:00-06:00"
  skip_final_snapshot       = false
  final_snapshot_identifier = var.billing_db_final_snapshot_identifier

  deletion_protection = var.billing_db_deletion_protection
}

resource "aws_ssm_parameter" "billing_database_connection_uri" {
  name        = var.billing_database_connection_uri_path
  description = "The connection URI for the Billing service database."
  type        = "SecureString"
  value       = "postgres://${urlencode("${aws_db_instance.billing_db.username}")}:${urlencode("${aws_db_instance.billing_db.password}")}@${aws_db_instance.billing_db.endpoint}/${aws_db_instance.billing_db.name}"
}

################################################################################
# MyMLOps backend 
################################################################################

resource "aws_iam_user" "mymlops_backend" {
  name = "MyMLOpsBackend"
}

resource "aws_iam_access_key" "mymlops_backend" {
  user = aws_iam_user.mymlops_backend.name
}

resource "aws_iam_user_policy" "mymlops_backend_policy" {
  name = "AssumeRoleForMyMLOpsBackend"
  user = aws_iam_user.mymlops_backend.name

  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": "sts:AssumeRole",
      "Resource": ["*"]
    }
  ]
}
EOF
}

resource "aws_ssm_parameter" "mymlops_backend_access_key_id" {
  name        = var.mymlops_backend_access_key_id_path
  description = "The AWS access key id for MyMLOps Backend."
  type        = "String"
  value       = aws_iam_access_key.mymlops_backend.id
}

resource "aws_ssm_parameter" "mymlops_backend_secret_access_key" {
  name        = var.mymlops_backend_secret_access_key_path
  description = "The AWS secret access key for MyMLOps Backend."
  type        = "SecureString"
  value       = aws_iam_access_key.mymlops_backend.secret
}

################################################################################
# MyMLOps backend: Contact service
################################################################################

resource "aws_iam_role" "mymlops_contact_service_role" {
  name = "MyMLOpsContactServiceRole"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          // Allow our AWS account to assume this role.
          AWS = "arn:aws:iam::045329000471:root"
        }
      },
    ]
  })
}

resource "aws_iam_policy" "mymlops_contact_service_policy" {
  name        = "SESAccessForMyMLOpsContactService"
  description = "Allows MyMLOps contact service to access SES services."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = [
          "ses:SendEmail"
        ]
        Effect   = "Allow"
        Resource = "*"
      },
    ]
  })
}

resource "aws_iam_role_policy_attachment" "mymlops_contact_service_policy_attachment" {
  role       = aws_iam_role.mymlops_contact_service_role.name
  policy_arn = aws_iam_policy.mymlops_contact_service_policy.arn
}

resource "aws_ssm_parameter" "mymlops_contact_role_arn" {
  name        = var.mymlops_contact_role_arn_path
  description = "The role ARN for the MyMLOps contact service."
  type        = "String"
  value       = aws_iam_role.mymlops_contact_service_role.arn
}

################################################################################
# MyMLOps backend: Tooling service
################################################################################

resource "aws_db_subnet_group" "mymlops_tooling_db" {
  name       = "mymlops_tooling_db_main"
  subnet_ids = data.terraform_remote_state.network.outputs.private_subnets
}

resource "random_password" "mymlops_tooling_db_password" {
  length           = 16
  special          = true
  override_special = "%@"
}

resource "aws_db_instance" "mymlops_tooling_db" {
  # Legacy name from restored database. Cannot be changed.
  name       = "mymlops_backend_db"
  identifier = "mymlops-tooling-db"

  engine            = "postgres"
  engine_version    = "13.7"
  instance_class    = var.mymlops_tooling_db_instance_class
  allocated_storage = var.mymlops_tooling_db_allocated_storage

  # Legacy username from restored database. Cannot be changed.
  username = "mymlops_backend_db"
  password = random_password.mymlops_tooling_db_password.result
  port     = "5432"

  vpc_security_group_ids = [aws_security_group.allow_traffic_from_cluster.id]
  db_subnet_group_name   = aws_db_subnet_group.mymlops_tooling_db.id

  maintenance_window        = "Mon:00:00-Mon:03:00"
  backup_window             = "03:01-05:00"
  backup_retention_period   = 7
  skip_final_snapshot       = false
  final_snapshot_identifier = var.mymlops_tooling_db_final_snapshot_identifier

  deletion_protection = var.mymlops_tooling_db_deletion_protection
  apply_immediately   = true
}

resource "aws_ssm_parameter" "mymlops_tooling_database_connection_uri" {
  name        = var.mymlops_tooling_database_connection_uri_path
  description = "The connection URI for the MyMLOps tooling service database."
  type        = "SecureString"
  value       = "postgres://${urlencode("${aws_db_instance.mymlops_tooling_db.username}")}:${urlencode("${aws_db_instance.mymlops_tooling_db.password}")}@${aws_db_instance.mymlops_tooling_db.endpoint}/${aws_db_instance.mymlops_tooling_db.name}"
}

################################################################################
# MyMLOps backend: Billing service
################################################################################

resource "aws_db_subnet_group" "mymlops_billing_db" {
  name       = "mymlops_billing_db_main"
  subnet_ids = data.terraform_remote_state.network.outputs.private_subnets
}

resource "random_password" "mymlops_billing_db_password" {
  length           = 16
  special          = true
  override_special = "%@"
}

resource "aws_db_instance" "mymlops_billing_db" {
  name       = "mymlops_billing_db"
  identifier = "mymlops-billing-db"

  engine            = "postgres"
  engine_version    = "13.7"
  instance_class    = var.mymlops_billing_db_instance_class
  allocated_storage = var.mymlops_billing_db_allocated_storage

  username = "mymlops_billing_db"
  password = random_password.mymlops_billing_db_password.result
  port     = "5432"

  vpc_security_group_ids = [aws_security_group.allow_traffic_from_cluster.id]
  db_subnet_group_name   = aws_db_subnet_group.mymlops_billing_db.id

  maintenance_window        = "Mon:00:00-Mon:03:00"
  backup_window             = "03:01-05:00"
  backup_retention_period   = 7
  skip_final_snapshot       = false
  final_snapshot_identifier = var.mymlops_billing_db_final_snapshot_identifier

  deletion_protection = var.mymlops_billing_db_deletion_protection
  apply_immediately   = true
}

resource "aws_ssm_parameter" "mymlops_billing_database_connection_uri" {
  name        = var.mymlops_billing_database_connection_uri_path
  description = "The connection URI for the MyMLOps billing service database."
  type        = "SecureString"
  value       = "postgres://${urlencode("${aws_db_instance.mymlops_billing_db.username}")}:${urlencode("${aws_db_instance.mymlops_billing_db.password}")}@${aws_db_instance.mymlops_billing_db.endpoint}/${aws_db_instance.mymlops_billing_db.name}"
}

################################################################################
# MyMLOps backend: Workspaces service
################################################################################

resource "aws_db_subnet_group" "mymlops_workspaces_db" {
  name       = "mymlops_workspaces_db_main"
  subnet_ids = data.terraform_remote_state.network.outputs.private_subnets
}

resource "random_password" "mymlops_workspaces_db_password" {
  length           = 16
  special          = true
  override_special = "%@"
}

resource "aws_db_instance" "mymlops_workspaces_db" {
  name       = "mymlops_workspaces_db"
  identifier = "mymlops-workspaces-db"

  engine            = "postgres"
  engine_version    = "13.7"
  instance_class    = var.mymlops_workspaces_db_instance_class
  allocated_storage = var.mymlops_workspaces_db_allocated_storage

  username = "mymlops_workspaces_db"
  password = random_password.mymlops_workspaces_db_password.result
  port     = "5432"

  vpc_security_group_ids = [aws_security_group.allow_traffic_from_cluster.id]
  db_subnet_group_name   = aws_db_subnet_group.mymlops_workspaces_db.id

  maintenance_window        = "Mon:00:00-Mon:03:00"
  backup_window             = "03:01-05:00"
  backup_retention_period   = 7
  skip_final_snapshot       = false
  final_snapshot_identifier = var.mymlops_workspaces_db_final_snapshot_identifier

  deletion_protection = var.mymlops_workspaces_db_deletion_protection
  apply_immediately   = true
}

resource "aws_ssm_parameter" "mymlops_workspaces_database_connection_uri" {
  name        = var.mymlops_workspaces_database_connection_uri_path
  description = "The connection URI for the MyMLOps workspaces service database."
  type        = "SecureString"
  value       = "postgres://${urlencode("${aws_db_instance.mymlops_workspaces_db.username}")}:${urlencode("${aws_db_instance.mymlops_workspaces_db.password}")}@${aws_db_instance.mymlops_workspaces_db.endpoint}/${aws_db_instance.mymlops_workspaces_db.name}"
}

# IAM role for EC2 service

resource "aws_iam_role" "mymlops_workspaces_service_role" {
  name = "MyMLOpsWorkspacesServiceRole"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          // Allow our AWS account to assume this role.
          AWS = "arn:aws:iam::045329000471:root"
        }
      },
    ]
  })
}

resource "aws_iam_policy" "mymlops_workspaces_service_policy" {
  name        = "EC2AccessForMyMLOpsWorkspacesService"
  description = "Allows MyMLOps workspaces service to access the EC2 services."

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = [
          "ec2:DescribeInstances",
          "ec2:RunInstances",
          "ec2:CreateTags",
          "ec2:StartInstances",
          "ec2:StopInstances",
          "ec2:TerminateInstances"
        ]
        Effect   = "Allow"
        Resource = "*"
      },
    ]
  })
}

resource "aws_iam_role_policy_attachment" "mymlops_workspaces_service_policy_attachment" {
  role       = aws_iam_role.mymlops_workspaces_service_role.name
  policy_arn = aws_iam_policy.mymlops_workspaces_service_policy.arn
}

resource "aws_ssm_parameter" "mymlops_workspaces_role_arn" {
  name        = var.mymlops_workspaces_role_arn_path
  description = "The role ARN for the MyMLOps workspaces service."
  type        = "String"
  value       = aws_iam_role.mymlops_workspaces_service_role.arn
}

# Network, subnet and security group for placing workspaces

data "aws_availability_zones" "available" {}

module "network" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "3.14.2"

  name                 = "mymlops-workspaces-network"
  cidr                 = "10.0.0.0/16"
  azs                  = data.aws_availability_zones.available.names
  public_subnets       = ["10.0.1.0/24"]
  enable_nat_gateway   = false
  enable_dns_hostnames = true
}

resource "aws_ssm_parameter" "mymlops_workspaces_subnet_id" {
  name        = var.mymlops_workspaces_subnet_id_path
  description = "The subnet ID for the MyMLOps workspaces service."
  type        = "String"
  value       = module.network.public_subnets[0]
}

resource "aws_security_group" "allow_traffic_to_workspace" {
  name        = "mymlops-workspace-sg"
  description = "Allow traffic to MyMLOps workspaces."
  vpc_id      = module.network.vpc_id

  ingress {
    description = "Allows traffic on all ports."
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port        = 0
    to_port          = 0
    protocol         = "-1"
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }
}

resource "aws_ssm_parameter" "mymlops_workspaces_security_group_id" {
  name        = var.mymlops_workspaces_security_group_id_path
  description = "The security group ID for the MyMLOps workspaces service."
  type        = "String"
  value       = aws_security_group.allow_traffic_to_workspace.id
}

# Redis cluster

resource "random_password" "mymlops_workspaces_redis_password" {
  length = 16
}

resource "aws_memorydb_user" "mymlops_workspaces_redis_user" {
  user_name     = "mymlops-workspaces"
  access_string = "on ~* &* +@all"

  authentication_mode {
    type      = "password"
    passwords = [random_password.mymlops_workspaces_redis_password.result]
  }
}

resource "aws_memorydb_acl" "mymlops_workspaces_redis_acl" {
  name       = "mymlops-workspaces-acl"
  user_names = [aws_memorydb_user.mymlops_workspaces_redis_user.id]
}

resource "aws_memorydb_subnet_group" "mymlops_workspaces_subnets" {
  name = "mymlops-workspaces-subnets"
  // The only supported AZs in us-east-1 are: [us-east-1d, us-east-1a, us-east-1c]
  subnet_ids = [
    data.terraform_remote_state.network.outputs.private_subnets[0],
    data.terraform_remote_state.network.outputs.private_subnets[2]
  ]
}

resource "aws_security_group" "allow_traffic_from_cluster_to_redis" {
  name        = "allow_traffic_from_cluster_to_redis"
  description = "Allow traffic from the cluster nodes to Redis."
  vpc_id      = data.terraform_remote_state.network.outputs.vpc_id

  ingress {
    description     = "Allows traffic on port 6379."
    from_port       = 6379
    to_port         = 6379
    protocol        = "tcp"
    security_groups = [data.terraform_remote_state.cluster.outputs.worker_security_group_id]
  }
}

resource "aws_memorydb_cluster" "mymlops_workspace_redis" {
  acl_name               = aws_memorydb_acl.mymlops_workspaces_redis_acl.name
  name                   = "mymlops-workspaces-cluster"
  node_type              = "db.t4g.small"
  engine_version         = "6.2"
  num_shards             = 1
  num_replicas_per_shard = 0
  port                   = 6379
  security_group_ids     = [aws_security_group.allow_traffic_from_cluster_to_redis.id]
  subnet_group_name      = aws_memorydb_subnet_group.mymlops_workspaces_subnets.id
}

resource "aws_ssm_parameter" "mymlops_workspaces_redis_connection_uri" {
  name        = var.mymlops_workspaces_redis_connection_uri_path
  description = "The Redis connecton URI for the MyMLOps workspaces service."
  type        = "String"
  value       = "rediss://${urlencode(aws_memorydb_user.mymlops_workspaces_redis_user.id)}:${urlencode(random_password.mymlops_workspaces_redis_password.result)}@${aws_memorydb_cluster.mymlops_workspace_redis.cluster_endpoint[0].address}:${aws_memorydb_cluster.mymlops_workspace_redis.cluster_endpoint[0].port}/0"
}
